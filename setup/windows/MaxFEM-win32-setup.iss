; ===========================================
; SOFTWARE REQUIREMENTS
; ===========================================
; 
; Before building the installer, you should have installed the following 
; software:
;   - Inno Setup
; Inno Setup can be downloaded from http://www.jrsoftware.org/isinfo.php
; We recommend the Inno Setup QuickStart Pack version 5.5.9 (unicode).
; 
; -------------------------------------------
; How to build the Windows installer
; -------------------------------------------
; 
; To build the Windows installer you must have a MaxFEM project folder (where 
; the full path will be referenced by MAXFEMDIR) with these folders and files:
;   - apps
;   - code
;   - config
;   - help
;   - images
;   - materialsDB
;   - plots
;   - setup
;   - solvers
;   - sources
;   - Changelog.txt
;   - LICENSE.txt
;   - README.txt
;   - remote-run-data.txt
; 
; The Windows installer can be built following these steps:
; 
; * You can edit, but not overwrite, the file MaxFEM-win32-setup.iss.
;   Keep MyAppVersion "X.Y.Z" to avoid errors in future use.
;   
;   1. You must check, and probably change, the MaxFEM version in the "About" 
;   section of the "Help" by editing the file MAXFEMDIR\help\e-About\index.html.
;   2. You must check, and probably change, the MaxFEM change log by editing 
;   the file MAXFEMDIR\Changelog.txt.
;   3. You must create the MaxFEM solvers by compiling from source. You can 
;   see how to do that in MAXFEMDIR\README.md.
;   4. Go to the folder MAXFEMDIR\setup\windows and open the file 
;   MaxFEM-win32-setup.iss using Inno Setup.
;   5. Set the version of MaxFEM by changing the value "X.Y.Z" of the 
;   variable MyAppVersion.
;   6. If necessary, set the revision of the installer package by changing 
;   the value "0" of the variable MySetupRevision.
;   7. If necessary, change the value of any other variable defined by 
;   "#define ...".
;   8. Build the installer by using the Inno Setup compile option. Remember 
;   that you can (and must) compile without saving changes.
;   9. The installer will be created in the folder MAXFEMDIR\setup\windows\output.

; Script generated by the Inno Script Studio Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "MaxFEM"
#define MyAppVersion "0.4.0"
#define MySetupRevision "0"
#define MyExtraFilename "win32-setup"
#define MyAppPublisher "Universidade de Santiago de Compostela"
#define MyAppCopyright "(C) 2012-2020 Universidade de Santiago de Compostela"
#define MyAppURL "http://www.usc.es/en/proxectos/maxfem/"
#define MyAppExeName "MaxFEM.py"
#define MyAppIcon "icon.MaxFEM.ico"
#define MyUninstallIcon "uninstall.MaxFEM.ico"
#define MyGettingStarted "a-Getting started\index.html"
#define MyUserGuide "b-User guide\index.html"
#define MyMathModels "c-Math models\index.html"
#define MyAbout "e-About\index.html"
#define MyChangelog "Changelog.txt"
#define MyLicense "LICENSE.txt"
#define MyReadme "README.md"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
WizardImageFile=compiler:WizModernImage-IS.bmp
WizardSmallImageFile=compiler:WizModernSmallImage-IS.bmp
AppName={#MyAppName}
AppVersion={#MyAppVersion}
AppCopyright={#MyAppCopyright}
AppId={50E50B1F-ACC3-43C9-B077-27A29D6F3175}
LicenseFile=..\..\{#MyLicense}
InfoBeforeFile=..\..\{#MyReadme}
SetupIconFile=..\..\images\{#MyAppIcon}
DefaultDirName={pf}\{#MyAppName}
EnableDirDoesntExistWarning=True
AllowNoIcons=yes
DefaultGroupName={#MyAppName}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
VersionInfoProductVersion={#MyAppVersion}
UninstallDisplayIcon={uninstallexe}
VersionInfoVersion={#MyAppVersion}.{#MySetupRevision}
VersionInfoCompany={#MyAppPublisher}
VersionInfoCopyright={#MyAppCopyright}
VersionInfoProductName={#MyAppName}
OutputDir=output
OutputBaseFilename={#MyAppName}-{#MyAppVersion}-{#MyExtraFilename}
Compression=lzma
SolidCompression=yes

[Languages]
Name: "English"; MessagesFile: "languages\English.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked
Name: "desktopicon\common"; Description: "{cm:IconForAll}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: exclusive
Name: "desktopicon\user"; Description: "{cm:IconForUser}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: exclusive unchecked
Name: "quicklaunchicon"; Description: "{cm:CreateQuickLaunchIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked; OnlyBelowVersion: 0,6.1
Name: "quicklaunchicon\common"; Description: "{cm:IconForAll}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: exclusive unchecked; OnlyBelowVersion: 0,6.1
Name: "quicklaunchicon\user"; Description: "{cm:IconForUser}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: exclusive unchecked; OnlyBelowVersion: 0,6.1

[Files]
Source: "..\..\Changelog.txt"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\..\LICENSE.txt"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\..\README.md"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\..\remote-run-data.txt"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\..\apps\*"; DestDir: "{app}\apps"; Flags: ignoreversion createallsubdirs recursesubdirs
Source: "..\..\code\*"; DestDir: "{app}\code"; Flags: ignoreversion createallsubdirs recursesubdirs; Excludes: "*.pyc"
Source: "..\..\config\*"; DestDir: "{app}\config"; Flags: ignoreversion createallsubdirs recursesubdirs
Source: "..\..\help\*"; DestDir: "{app}\help"; Flags: ignoreversion createallsubdirs recursesubdirs; Excludes: "*.notempty"
Source: "..\..\images\*"; DestDir: "{app}\images"; Flags: ignoreversion createallsubdirs recursesubdirs
Source: "..\..\materialsDB\*"; DestDir: "{app}\materialsDB"; Flags: ignoreversion createallsubdirs recursesubdirs
Source: "..\..\plots\*"; DestDir: "{app}\plots"; Flags: ignoreversion createallsubdirs recursesubdirs
Source: "..\..\solvers\*"; DestDir: "{app}\solvers"; Flags: createallsubdirs recursesubdirs; Excludes: "*.notempty"
Source: "..\..\sources\*"; DestDir: "{app}\sources"; Flags: ignoreversion createallsubdirs recursesubdirs; Excludes: "*.notempty"
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{group}\{#MyAppName}"; Filename: "{app}\code\{#MyAppExeName}"; WorkingDir: "{app}\code"; IconFilename: "{app}\images\{#MyAppIcon}"
Name: "{group}\{cm:ProgramOnTheWeb,{#MyAppName}}"; Filename: "{#MyAppURL}"
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"; IconFilename: "{app}\images\{#MyUninstallIcon}"
Name: "{group}\Help\Getting started"; Filename: "{app}\help\{#MyGettingStarted}"; WorkingDir: "{app}\help"
Name: "{group}\Help\User guide"; Filename: "{app}\help\{#MyUserGuide}"; WorkingDir: "{app}\help"
Name: "{group}\Help\Math models"; Filename: "{app}\help\{#MyMathModels}"; WorkingDir: "{app}\help"
Name: "{group}\Help\About"; Filename: "{app}\help\{#MyAbout}"; WorkingDir: "{app}\help"
Name: "{commondesktop}\{#MyAppName}"; Filename: "{app}\code\{#MyAppExeName}"; IconFilename: "{app}\images\{#MyAppIcon}"; Comment: "{#MyAppName}"; Tasks: desktopicon\common
Name: "{userdesktop}\{#MyAppName}"; Filename: "{app}\code\{#MyAppExeName}"; IconFilename: "{app}\images\{#MyAppIcon}"; Comment: "{#MyAppName}"; Tasks: desktopicon\user
Name: "{commonappdata}\Microsoft\Internet Explorer\Quick Launch\{#MyAppName}"; Filename: "{app}\code\{#MyAppExeName}"; IconFilename: "{app}\images\{#MyAppIcon}"; Comment: "{#MyAppName}"; Tasks: quicklaunchicon\common
Name: "{userappdata}\Microsoft\Internet Explorer\Quick Launch\{#MyAppName}"; Filename: "{app}\code\{#MyAppExeName}"; IconFilename: "{app}\images\{#MyAppIcon}"; Comment: "{#MyAppName}"; Tasks: quicklaunchicon\user

[Run]
Filename: "{app}\code\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; WorkingDir: "{app}\code"; Flags: shellexec postinstall skipifsilent
Filename: "{app}\{#MyChangelog}"; WorkingDir: "{app}"; Flags: shellexec skipifdoesntexist postinstall skipifsilent

[ThirdParty]
UseRelativePaths=True

[UninstallDelete]
Type: filesandordirs; Name: "{app}\code"
